#!/usr/bin/env zsh
#
# Zsh Prezto Theme by PeRDy based on Garrett's one (https://github.com/chauncey-garrett/zsh-prompt-garrett)
# https://github.com/PeRDy/zsh-prompt-perdy
#

# Load dependencies.
pmodload 'helper'

# Truncate the PWD display, if needed.
function prompt_perdy_pwd {
  _prompt_perdy_current_dir="${_prompt_perdy_color_pwd}${(@j:/:)${(@s:/:)$(print -P '%5~')}}"
}

# Notify of command completion after n seconds has passed.
function prompt_perdy_precmd_notification {

  # Trigger a notification after n seconds have elapsed.
  _prompt_perdy_delay_after_notification=5

  # Determine time elapsed.
  _prompt_perdy_precmd_time_elapsed_start=${_prompt_perdy_preexec_time:-$(date +'%s')}
  _prompt_perdy_precmd_time_elapsed_stop=$(date +'%s')
  let _prompt_perdy_precmd_time_elapsed=$_prompt_perdy_precmd_time_elapsed_stop-$_prompt_perdy_precmd_time_elapsed_start

  # n seconds have passed, so notify!
  if [ $_prompt_perdy_precmd_time_elapsed -gt $_prompt_perdy_delay_after_notification ]; then

    # Notify via tput while on ssh.
    if [[ -n "$SSH_CLIENT" || -n "$SSH2_CLIENT" ]]; then
      tput bel

    # Use local notifications.
    else

      # On OS X, notify with terminal-notifier.
      if [[ "$OSTYPE" == darwin* ]] && (( $+commands[terminal-notifier] )); then
        terminal-notifier \
          -title "${_prompt_perdy_preexec_cmd:-Unknown command}" \
          -message "Required $_prompt_perdy_precmd_time_elapsed s" \
          -sound Tink

      # On OS X, notify with growlnotify.
      elif [[ "$OSTYPE" == darwin* ]] && (( $+commands[growlnotify] )); then
        growlnotify \
          -n "Terminal" \
          -t "${_prompt_perdy_preexec_cmd:-Unknown command}" \
          -m "Required $_prompt_perdy_precmd_time_elapsed s"

      # On linux, notify with notify-send.
      elif [[ "$OSTYPE" == linux-gnu ]] && (( $+commands[notify-send] )); then
        notify-send \
          "${_prompt_perdy_preexec_cmd:-Unknown command}" \
          "Required $_prompt_perdy_precmd_time_elapsed s"

      # Notify via tput.
      else
        tput bel
      fi
    fi
  fi
}

# A function executed whenever the directory is changed.
function prompt_perdy_chpwd {

  # List the contents of the new directory.
  if is-callable 'dircolors'; then

    # GNU flavor ls.
    if zstyle -t ':prezto:module:utility:ls' color; then
      ls --group-directories-first --color=auto
    else
      ls -F
    fi
  else

    # BSD flavor ls.
    if zstyle -t ':prezto:module:utility:ls' color; then
      ls -G
    else
      ls -F
    fi
  fi
}

# Functions called before command execution.
function prompt_perdy_preexec {

  # Define timer and command for notification.
  export _prompt_perdy_preexec_time=$(date +'%s')
  export _prompt_perdy_preexec_cmd="\$ $1"

  # Ensure terminal code isn't colored from prompt.
  print -n "$reset_color"
}

# Functions called before each prompt is displayed.
function prompt_perdy_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Show number of background jobs.
  _prompt_perdy_number_jobs="%(1j.${_prompt_perdy_color_prompt}J:${cyan}%j .)"

  # Format PWD.
  prompt_perdy_pwd

  # Trigger a notification after x time has elapsed.
  eval prompt_perdy_precmd_notification

  # Get Git repository info.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Python info.
  if (( $+functions[python-info] )); then
    python-info
  fi

  #
  # Report Git status
  #
  if [[ -n $git_info ]]; then
    local _prompt_perdy_git="[ ${git_info[remote_status]}${git_info[prompt_info]}${git_info[local_status]}${git_info[sha]} ]"
  else
    local _prompt_perdy_git=''
  fi

  #
  # Report Python virtualenv
  #
  if [[ -n $python_info ]]; then
    local _prompt_perdy_python="[ $python_info[virtualenv] ]"
  else
    local _prompt_perdy_python=''
  fi

  #
  # Add a line to prompt for visibility.
  #

  # Determine the width.
  local _prompt_perdy_width_terminal
  (( _prompt_perdy_width_terminal= ${COLUMNS} - 1 ))

  # Determine the length needed for _prompt_perdy_space.
  # NOTE: Be sure not to include the ${(e)_prompt_perdy_space} portion or it won't work.
  local _prompt_perdy_line1="${_prompt_perdy_upper_left_corner}${_prompt_perdy_python}( ${_prompt_perdy_current_dir} )${_prompt_perdy_git}( ${_prompt_perdy_location} )${_prompt_perdy_upper_right_corner}"
  local zero='%([BSUbfksu]|([FB]|){*})'
  local _prompt_perdy_width_line1=${#${(S%%)_prompt_perdy_line1//$~zero/}}

  # Calculate the padding.
  local _prompt_perdy_space_padding
  (( _prompt_perdy_space_padding= ${_prompt_perdy_width_terminal} - ${_prompt_perdy_width_line1} ))

  # Add the correct number of characters.
  local _prompt_perdy_space_character="${_prompt_perdy_altchar_padding}"
  eval _prompt_perdy_space="${_prompt_perdy_color_prompt}\${(l.${_prompt_perdy_space_padding}..${_prompt_perdy_space_character}.)}"

  # Prompt line 1 (set above PROMPT, below).
  print -P '${_prompt_perdy_altchar_enable}${_prompt_perdy_color_prompt}${_prompt_perdy_upper_left_corner}${_prompt_perdy_python}( ${_prompt_perdy_current_dir} ${_prompt_perdy_color_prompt})${_prompt_perdy_git}${_prompt_perdy_altchar_enter}${(e)_prompt_perdy_space}${_prompt_perdy_altchar_leave}( ${_prompt_perdy_location}${_prompt_perdy_color_prompt} )${_prompt_perdy_upper_right_corner}'
}

function prompt_perdy_user_prompt {
  # Root user.
  if [[ "$EUID" = "0" ]] || [[ "$USER" = 'root' ]]; then

    # Set colors.
    eval _prompt_perdy_color_user=${3:-'${red}'}
    eval _prompt_perdy_color_host=${3:-'${red}'}
    eval _prompt_perdy_color_prompt=${3:-'${red}'}

    # Set style.
    eval _prompt_perdy_user='%S${_prompt_perdy_color_user}%n%s'
    eval _prompt_perdy_host='${_prompt_perdy_color_host}%m' # hostname up to first . (dot) (use %M for full hostname)
    eval _prompt_perdy_location='${_prompt_perdy_user}${cyan}@${_prompt_perdy_host}' # user@host.name

  # On SSH.
  elif [[ -n "$SSH_CLIENT" || -n "$SSH2_CLIENT" ]]; then

    # Set colors.
    eval _prompt_perdy_color_user=${3:-'${green}'}
    eval _prompt_perdy_color_host=${3:-'${yellow}'}
    eval _prompt_perdy_color_prompt=${3:-'${yellow}'}

    # Set style.
    eval _prompt_perdy_user='%S${_prompt_perdy_color_user}%n%s'
    eval _prompt_perdy_host='${_prompt_perdy_color_host}%m' # hostname up to first . (dot) (use %M for full hostname)
    eval _prompt_perdy_location='${_prompt_perdy_user}${cyan}@${_prompt_perdy_host}' # user@host.name

  # Normal user.
  else

    # Set colors.
    eval _prompt_perdy_color_user=${1:-'${green}'}
    eval _prompt_perdy_color_host=${1:-'${green}'}
    eval _prompt_perdy_color_prompt=${1:-'${grey}'}

    # Set style.
    eval _prompt_perdy_user=''
    eval _prompt_perdy_host='${_prompt_perdy_color_host}%m' # hostname up to first . (dot) (use %M for full hostname)
    eval _prompt_perdy_location='${_prompt_perdy_user}${cyan}@${_prompt_perdy_host}' # user@host.name
  fi

}

function prompt_perdy_prezto_style {
  #
  # Report git info.
  # NOTE: Listed in order in which the information will appear in the prompt.
  #

  # Git verbose data (commit counts, etc.)
  # zstyle ':prezto:module:git:info' verbose 'yes'

  # Git prompt info.
  zstyle ':prezto:module:git:info:branch'    format "${cyan}${_prompt_perdy_color_prompt}:${green}%b"
  zstyle ':prezto:module:git:info:remote'    format ""
  zstyle ':prezto:module:git:info:action'    format "${yellow} %s"
  zstyle ':prezto:module:git:info:position'  format "${red} %p"

  # Git commit SHA.
  zstyle ':prezto:module:git:info:commit'    format "${yellow} %.7c${_prompt_perdy_color_prompt}"

  # Git remote status.
  zstyle ':prezto:module:git:info:behind'    format "${magenta}⬇ "
  zstyle ':prezto:module:git:info:ahead'     format "${magenta}⬆ "
  zstyle ':prezto:module:git:info:diverged'  format "${magenta}⥮ "
  zstyle ':prezto:module:git:info:stashed'   format "${cyan}✭ "

  # Git local status.
  zstyle ':prezto:module:git:info:clean'     format ""
  zstyle ':prezto:module:git:info:dirty'     format "${_prompt_perdy_color_prompt} | "
  zstyle ':prezto:module:git:info:added'     format "${green}✚"
  zstyle ':prezto:module:git:info:deleted'   format "${red}✗"
  zstyle ':prezto:module:git:info:modified'  format "${blue}✱"
  zstyle ':prezto:module:git:info:renamed'   format "${magenta}➜"
  zstyle ':prezto:module:git:info:unmerged'  format "${yellow}═"
  zstyle ':prezto:module:git:info:untracked' format "${white}◼"

  # Git prompt styles.
  zstyle ':prezto:module:git:info:keys' format \
    'prompt_info'    "%b" \
    'rprompt'        "%R" \
    'local_status'   "%C%D%a%d%m%r%U%u" \
    'remote_status'  "%B%A%S" \
    'sha'            "%c" \

  #
  # Report Python virtualenv
  #
  zstyle ':prezto:module:python:info:virtualenv' format "${yellow}ⓥ %v${_prompt_perdy_color_prompt}"

  #
  # Command line editor info.
  #

  # Base style.
  zstyle ':prezto:module:editor:info:keymap:primary' format "${cyan}❱%(?.${_prompt_perdy_color_prompt}.${red})❱❱ "

  # Vim insert mode.
  # zstyle ':prezto:module:editor:info:keymap:primary:insert' format "${red}I "

  # Vim overwrite mode.
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format "${red}♺ "

  # Vim normal (command) mode.
  zstyle ':prezto:module:editor:info:keymap:alternate' format "${cyan}❰%(?.${_prompt_perdy_color_prompt}.${red})❰❰ "

  # Tab completion mode.
  zstyle ':prezto:module:editor:info:completing' format "${red}..."
}

# Configure the prompt.
function prompt_perdy_setup {

  # Load necessary modules.
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Add hooks for calling preexec, precmd & chpwd.
  autoload -Uz add-zsh-hook
  add-zsh-hook preexec prompt_perdy_preexec
  add-zsh-hook precmd prompt_perdy_precmd
  add-zsh-hook chpwd prompt_perdy_chpwd

  #
  # Colors
  #

  # Alias the colors.
  [[ -z $(functions colors) ]] && autoload -U colors && colors
  for color in black red green yellow blue magenta cyan white grey; do

    # Normal colors.
    eval $color='%F{${(L)color}}'

    # Bold colors.
    eval ${color}_bold='%B{${(L)color}}'
  done

  # Color scheme.
  eval _prompt_perdy_color_pwd=\$\{${2:-'${blue}'}\}
  eval _prompt_perdy_color_line_number=${5:-'${magenta}'}
  eval _prompt_perdy_color_time=${6:-'${green}'}
  eval _prompt_perdy_color_git_branch=${7:-'${green}'}
  eval _prompt_perdy_color_git_sha=${8:-'${yellow}'}

  # Determine prompt, user and host colors.
  prompt_perdy_user_prompt
  
  # Report return code.
  eval _prompt_perdy_return_code='%(?..${red}%? ⏎  ) '

  # Report local time.
  eval _prompt_perdy_current_time='${green}%*'    # 24 hour time format, second precise
  # eval _prompt_perdy_current_time='${green}%T'    # 24 hour time format
  # eval _prompt_perdy_current_time='${green}%t'    # AM/PM time format

  # Report terminal line number.
  eval _prompt_perdy_line_number='${green}+${magenta}%!'
  
  #
  # Use the extended character set, if available.
  #
  typeset -A altchar
  set -A altchar ${(s..)terminfo[acsc]}

  _prompt_perdy_altchar_enable="%{$terminfo[enacs]%}"
  _prompt_perdy_altchar_enter="%{$terminfo[smacs]%}"
  _prompt_perdy_altchar_leave="%{$terminfo[rmacs]%}"

  # Character used to draw line.
  _prompt_perdy_altchar_padding=${altchar[q]:--}

  # Upper left corner: ┌
  _prompt_perdy_upper_left_corner=${_prompt_perdy_altchar_enter}${altchar[l]:--}${altchar[q]:--}${_prompt_perdy_altchar_leave}

  # Upper right corner: ┐
  _prompt_perdy_upper_right_corner=${_prompt_perdy_altchar_enter}${altchar[q]:--}${altchar[k]:--}${_prompt_perdy_altchar_leave}

  # Lower left corner: └
  _prompt_perdy_lower_left_corner=${_prompt_perdy_altchar_enter}${altchar[m]:--}${altchar[q]:--}${_prompt_perdy_altchar_leave}

  # Upper right corner: ┘
  _prompt_perdy_lower_right_corner=${_prompt_perdy_altchar_enter}${altchar[q]:--}${altchar[j]:--}${_prompt_perdy_altchar_leave}

  # Define prezto style
  prompt_perdy_prezto_style

  #
  # Print out the prompt.
  #
  # Left prompt; base shell level.
  if (( $SHLVL == 1 )); then
    export PROMPT='${_prompt_perdy_altchar_enable}${_prompt_perdy_color_prompt}${_prompt_perdy_lower_left_corner}${editor_info[keymap]}'

  # Left prompt; nested shell level.
  else
    export PROMPT='${_prompt_perdy_color_prompt}${_prompt_perdy_lower_left_corner}( ${cyan}$SHLVL ${_prompt_perdy_color_prompt})${editor_info[keymap]}'
  fi

  # Right prompt.
  export RPROMPT='${editor_info[alternate]}${editor_info[overwrite]}${_prompt_perdy_return_code}${_prompt_perdy_number_jobs}${_prompt_perdy_line_number} ${_prompt_perdy_current_time} %(?.${_prompt_perdy_color_prompt}.${red})❰${_prompt_perdy_color_prompt}${_prompt_perdy_lower_right_corner}'

  # Continuation prompt.
  export PROMPT2='(%_) ${editor_info[keymap]}'

  # Selection prompt.
  export PROMPT3='
(?) ${editor_info[keymap]}'

  # Execution trace prompt.
  export PROMPT4='${yellow}+${blue}%N ${green}@${magenta}%i ${reset_color}'

  # Autocorrection prompt.
  export SPROMPT='
${_prompt_perdy_color_prompt}Correct ${red}%R${_prompt_perdy_color_prompt} to ${green}%r${_prompt_perdy_color_prompt} ? [nyae] '

  # Backup root prompt for Bash.
  # export SUDO_PS1="\[\u@\h\] \w \$ "
}

prompt_perdy_setup "$@"

